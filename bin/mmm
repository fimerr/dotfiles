#!/usr/bin/env zsh
#
### Search and render cheatsheets in command line
###   mmm                list all cheatsheets

search_cheatsheets_filename() {
    local query=()
    while read -r line; do
        query+=( "$line" )
    done < <(
        cd ${DOTFILES_CHEATSHEETS_ROOT} &&
        fd "" --type f --follow --exec basename {} .md \; | sort |
        fzf --query "$@"
    )
    _execute_cheatsheet "${query[@]}"
}

search_cheatsheets_content() {
    local query=()
    while read -r line; do
        query+=( "$line" )
    done < <(
        cd ${DOTFILES_CHEATSHEETS_ROOT} &&
        rg -w --color=always --line-number --no-heading --smart-case "${*:-}" |
        sed 's/.md//' |  # cut suffix ".md" from filename
        fzf --delimiter : \
        --preview 'bat --theme=Dracula --style=plain --color=always --tabs=4 --wrap=auto {1}.md --highlight-line {2}' \
        --preview-window down,75%:wrap,border,follow,+{2}+3/3,~3
    )
    # remove useless part
    query[2]=$(echo ${query[2]} | sed 's/:.*//')

    _execute_cheatsheet "${query[@]}"
}

# input:
#   {query_text}
#   {query_text}, {matched_line}
_execute_cheatsheet() {
    [[ "$#" > 2 ]] && echo "[ERROR] only receive two parameters: {query_text}, {matched_line}" && exit 1

    local query_text="$(echo -e $1 | xargs echo -n)"
    local matched_line="$(echo -e $2 | xargs echo -n)"
    case "${query_text}" in
        *\. )
            search_cheatsheets_filename ${query_text:0:-1}
            ;;
        *\? )
            search_cheatsheets_content ${query_text:0:-1}  # cut last char ?
            ;;
        * )
            matched_line=${matched_line%.md}  # remove suffix ".md"
            [[ -n ${matched_line} ]] && glow -s ${DOTFILES_ROOT}/misc/glow_style.json ${DOTFILES_CHEATSHEETS_ROOT}/${matched_line}.md
            ;;
    esac
}

fzf_header=$(cat <<EOF
[foo.] search filename, [foo?] search content; [CTRL-E] edit, [CTRL-J/K] scroll preview
EOF
)
FZF_DEFAULT_OPTS="--height=72% --info=hidden --ansi --color=16 --layout=reverse --extended --cycle --exact --no-sort --tabstop=4 \
--header '${fzf_header}' \
--preview-window down,75%:wrap,border,follow \
--preview 'bat --theme=Dracula --style=plain --color=always --tabs=4 --wrap=auto ${DOTFILES_CHEATSHEETS_ROOT}/{}.md 2>/dev/null' \
--print-query \
--bind='ctrl-j:preview-down' \
--bind='ctrl-k:preview-up' \
--bind='ctrl-e:execute:${EDITOR:-vim} ${DOTFILES_CHEATSHEETS_ROOT}/{1}.md'
"
LESS='-iMsR -j3'

[[ "$#" > 1 ]] && echo "ERROR: only accpet ONE parameter!" && exit 1
case "$1" in
    -help | -h )
        _print_help "$(realpath $0)"
        exit 0
        ;;
    * )
        ;;
esac
search_cheatsheets_filename "$*"
